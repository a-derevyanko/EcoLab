apply plugin: 'war'
apply plugin: 'org.springframework.boot'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:+")
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'http://maven.vaadin.com/vaadin-addons'
    }
    maven {
        url 'http://jasperreports.sourceforge.net/maven2/'
    }
    maven {
        url 'http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/'
    }
}

dependencies {
    compile project (':eko-server')
    compile project (':ekolab-vaadin-client')
    //compile project (':ekolab-server')
    compile group: 'org.apache.commons',            name: 'commons-lang3',                      version: '+'
    compile(group: 'com.vaadin',                   name: 'vaadin-spring-boot-starter',          version: '+') {
        exclude group: 'com.vaadin'
    }

    compile group: 'org.vaadin.spring.extensions',  name: 'vaadin-spring-ext-security',         version: '+'
    compile group: 'org.vaadin.spring.extensions',  name: 'vaadin-spring-ext-boot',             version: '+'

    compile group: 'org.springframework.boot',  name: 'spring-boot-configuration-processor',    version: '+'

    runtime group: 'org.springframework.boot',  name: 'spring-boot-starter-aop',                version: '1.5.3.RELEASE'
    runtime group: 'de.codecentric',            name: 'spring-boot-admin-starter-client',       version: '1.5.3'

    runtime group: 'net.sf.jasperreports',      name: 'jasperreports-fonts',                    version: '6.0.0'

    runtime group: 'com.github.aleksandy',      name: 'petrovich',                              version: '1.0.0'
}

war {
    baseName = 'EkoLab-Vaddin'
    version =  '0.0.1'
}

springBoot.mainClass = 'org.ekolab.client.vaadin.server.VaadinApplication'

/**
 * Линковка ClassPath, чтобы обойти ошибку Windows 'CreateProcess error=206, Имя файла или его расширение имеет слишком большую длину'
 */
task pathingJar(type: Jar) {
    dependsOn configurations.runtime
    appendix = 'pathing'

    doFirst {
        manifest {
            attributes "Class-Path": configurations.runtime.files.collect {
                it.toURI().toString().replaceFirst("/file:/+/", "/")
            }.join(' ')
        }
    }
}

bootRun {
    dependsOn pathingJar
    doFirst {
        classpath = files("$buildDir/classes/main", "$buildDir/resources/main", pathingJar.archivePath)
    }
    systemProperties(System.properties)
    jvmArgs = ["-Dspring.profiles.active=demo,h2", "-Dorg.jooq.no-logo=true"]
    addResources = true
}

bootRepackage {
    enabled = true
    executable = true
}
clean.delete 'src/main/webapp'

task copyVaadinClient(type: Copy) {
    from project (':ekolab-vaadin-client').file('/src/main/webapp')
    into 'src/main/webapp'
}

compileJava.dependsOn(copyVaadinClient)
compileJava.dependsOn(processResources)