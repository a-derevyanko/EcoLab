buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id "org.springframework.boot" version "2.1.0.RELEASE"
}

apply plugin: 'war'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'http://maven.vaadin.com/vaadin-addons'
    }
    maven {
        url 'http://jasperreports.sourceforge.net/maven2/'
    }
    maven {
        url 'http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/'
    }
}

dependencies {
    compile project (':ecolab-vaadin-server')
    compile group: 'de.huxhorn.lilith',         name: 'de.huxhorn.lilith.3rdparty.junique',     version: '1.0.4'

    providedRuntime group: 'org.springframework.boot',  name: 'spring-boot-starter-jetty',      version: '2.1.0.RELEASE'
}

springBoot {
    buildInfo()
}

bootWar {
    baseName = 'EcoLab-Vaddin-Desktop'
    version =  '0.0.1'
    mainClassName = 'org.ecolab.client.vaadin.desktop.DesktopVaadinApplication'
}

/**
 * Линковка ClassPath, чтобы обойти ошибку Windows 'CreateProcess error=206, Имя файла или его расширение имеет слишком большую длину'
 */
task pathingJar(type: Jar) {
    dependsOn configurations.runtime
    appendix = 'pathing'

    doFirst {
        manifest {
            attributes "Class-Path": configurations.runtime.files.collect {
                it.toURI().toString().replaceFirst("/file:/+/", "/")
            }.join(' ')
        }
    }
}

bootRun {
    dependsOn pathingJar
    doFirst {
        classpath = files(sourceSets.main.output.files, pathingJar.archivePath)
    }
    systemProperties(System.properties)
    jvmArgs = ["-Dspring.profiles.active=production,custom,h2", "-Dorg.jooq.no-logo=true"]
    sourceResources sourceSets.main
}

clean.delete 'src/main/webapp'

task copyVaadinClient(type: Copy) {
    from project (':ecolab-vaadin-client').file('/src/main/webapp')
    into 'src/main/webapp'
}

compileJava.dependsOn(copyVaadinClient)
compileJava.dependsOn(processResources)