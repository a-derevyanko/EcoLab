
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.0.RELEASE")
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "2.2.0"
}

apply plugin: 'org.springframework.boot'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'http://maven.vaadin.com/vaadin-addons'
    }
    maven {
        url 'http://jasperreports.sourceforge.net/maven2/'
    }
    maven {
        url 'http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/'
    }
}

dependencies {
    compile project (':ecolab-vaadin-server')
    compile group: 'de.huxhorn.lilith',         name: 'de.huxhorn.lilith.3rdparty.junique',     version: '1.0.4'
    compile("org.springframework.boot:spring-boot-starter-jetty:2.0.0.BUILD-SNAPSHOT")
}

springBoot.mainClassName = 'org.ecolab.client.vaadin.desktop.DesktopVaadinApplication'


/**
 * Линковка ClassPath, чтобы обойти ошибку Windows 'CreateProcess error=206, Имя файла или его расширение имеет слишком большую длину'
 */
task pathingJar(type: Jar) {
    dependsOn configurations.runtime
    appendix = 'pathing'

    doFirst {
        manifest {
            attributes "Class-Path": configurations.runtime.files.collect {
                it.toURI().toString().replaceFirst("/file:/+/", "/")
            }.join(' ')
        }
    }
}

bootRun {
    dependsOn pathingJar
    doFirst {
        classpath = files(sourceSets.main.output.files, pathingJar.archivePath)
    }
    systemProperties(System.properties)
    jvmArgs = ["-Dspring.profiles.active=production,custom,h2", "-Dorg.jooq.no-logo=true"]
}

springBoot {
    buildInfo()
}

gitProperties {
    gitPropertiesDir = new File("${project.projectDir.path}/src/main/resources")
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "GMT+03:00"
}

clean.delete 'src/main/webapp'

task copyVaadinClient(type: Copy) {
    from project (':ecolab-vaadin-client').file('/src/main/webapp')
    into 'src/main/webapp'
}

compileJava.dependsOn(copyVaadinClient)
compileJava.dependsOn(processResources)